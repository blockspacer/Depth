# Define the presents flag of the 'DepthApplicationConsole' static library.
SET(DEPTH_APPLICATION_CONSOLE_PRESENT 1)

# Define 'DepthApplicationConsole' project name.
SET(DEPTH_APPLICATION_CONSOLE "DepthApplicationConsole")

# Define 'DepthApplicationConsole' project.
PROJECT(${DEPTH_APPLICATION_CONSOLE} CXX)

# Load versions.
FILE(READ "${DEPTH_VERSIONS}/DepthApplicationConsole.txt" DEPTH_APPLICATION_CONSOLE_VERSION)
FILE(READ "${DEPTH_VERSIONS}/DepthApplicationConsole-so.txt" DEPTH_APPLICATION_CONSOLE_SOVERSION)

# Include directories definition for the 'DepthApplicationConsole' static library.
INCLUDE_DIRECTORIES(${DEPTH})

# Prepare sources directories for the 'DepthApplicationConsole' static library.
LIST(APPEND DEPTH_APPLICATION_CONSOLE_SOURCES_DIRS
        "${DEPTH_SOURCE}/system/sys-All/application-console"
        "${DEPTH_SOURCE}/system/sys-Cygwin/application-console"
        "${DEPTH_SOURCE}/system/sys-Dummy/application-console"
        "${DEPTH_SOURCE}/system/sys-Linux/application-console"
        "${DEPTH_SOURCE}/system/sys-Unix/application-console"
        "${DEPTH_SOURCE}/system/sys-Windows/application-console"
        "${DEPTH_SOURCE}/system/sys-WindowsMobile/application-console")

# Collect sources for the 'DepthApplicationConsole' static library.
FOREACH(DEPTH_APPLICATION_CONSOLE_SOURCES_DIR ${DEPTH_APPLICATION_CONSOLE_SOURCES_DIRS})
  FILE(GLOB DEPTH_APPLICATION_CONSOLE_SOURCES_FILES RELATIVE "${CMAKE_SOURCE_DIR}" "${DEPTH_APPLICATION_CONSOLE_SOURCES_DIR}/*.cpp")
  LIST(APPEND DEPTH_APPLICATION_CONSOLE_SOURCES ${DEPTH_APPLICATION_CONSOLE_SOURCES_FILES})
ENDFOREACH(DEPTH_APPLICATION_CONSOLE_SOURCES_DIR)

# Add the 'DepthApplicationConsole' static library.
ADD_LIBRARY(${DEPTH_APPLICATION_CONSOLE} STATIC ${DEPTH_APPLICATION_CONSOLE_SOURCES})

# Set compiler flags.
SET_PROPERTY(TARGET ${DEPTH_APPLICATION_CONSOLE} APPEND PROPERTY COMPILE_DEFINITIONS "__BUILD_STATIC_LIB__" "DEPTH_APPLICATION_CONSOLE_VERSION=${DEPTH_APPLICATION_CONSOLE_VERSION}" "DEPTH_APPLICATION_CONSOLE_SOVERSION=${DEPTH_APPLICATION_CONSOLE_SOVERSION}")

# Set version and soversion of the static library.
SET_TARGET_PROPERTIES(${DEPTH_APPLICATION_CONSOLE} PROPERTIES VERSION "${DEPTH_APPLICATION_CONSOLE_VERSION}" SOVERSION "${DEPTH_APPLICATION_CONSOLE_SOVERSION}")

# Add dependence from the 'DepthBase' shared library to the 'DepthApplicationConsole' static library.
ADD_DEPENDENCIES(${DEPTH_APPLICATION_CONSOLE} ${DEPTH_BASE})
# Add dependence from the 'DepthApplication' shared library to the 'DepthApplicationConsole' static library.
ADD_DEPENDENCIES(${DEPTH_APPLICATION_CONSOLE} ${DEPTH_APPLICATION})

# Link the 'DepthApplicationConsole' static library with the system libraries.
TARGET_LINK_LIBRARIES(${DEPTH_APPLICATION_CONSOLE} ${CMAKE_SYSTEM_LIBRARIES})
# Link the 'DepthApplicationConsole' static library with the 'DepthBase' shared library.
TARGET_LINK_LIBRARIES(${DEPTH_APPLICATION_CONSOLE} ${DEPTH_BASE})
# Link the 'DepthApplicationConsole' static library with the 'DepthApplication' shared library.
TARGET_LINK_LIBRARIES(${DEPTH_APPLICATION_CONSOLE} ${DEPTH_APPLICATION})

# Install the 'DepthApplicationConsole' static library.
INSTALL(TARGETS ${DEPTH_APPLICATION_CONSOLE}
        RUNTIME DESTINATION ${DEPTH_BIN}
        LIBRARY DESTINATION ${DEPTH_LIB_SHARED}
        ARCHIVE DESTINATION ${DEPTH_LIB_STATIC})

# Add 'DepthApplicationConsole' project to the 'Depth' dependencies.
LIST(APPEND DEPTH_TARGETS ${DEPTH_APPLICATION_CONSOLE})
