# Define the presents flag of the 'DepthBase' shared library examples.
SET(DEPTH_BASE_EXAMPLES_PRESENT 1)

# Define 'DepthBase' examples project name.
SET(DEPTH_BASE_EXAMPLES "DepthBase-examples")

# Define 'DepthBase' examples project.
PROJECT(${DEPTH_BASE_EXAMPLES})

# Prepare sources directories for the 'DepthBase' shared library examples.
LIST(APPEND DEPTH_BASE_EXAMPLES_SOURCES_DIRS
        "${DEPTH_EXAMPLES}/algorithms"
        "${DEPTH_EXAMPLES}/base"
        "${DEPTH_EXAMPLES}/common"
        "${DEPTH_EXAMPLES}/configure"
        "${DEPTH_EXAMPLES}/containers"
        "${DEPTH_EXAMPLES}/errors"
        "${DEPTH_EXAMPLES}/functors"
        "${DEPTH_EXAMPLES}/math"
        "${DEPTH_EXAMPLES}/memory"
        "${DEPTH_EXAMPLES}/stream"
        "${DEPTH_EXAMPLES}/string"
        "${DEPTH_EXAMPLES}/time"
        "${DEPTH_EXAMPLES}/tuple"
        "${DEPTH_EXAMPLES}/traits"
        "${DEPTH_EXAMPLES}/utility")

# Collect and parse sources for the 'DepthBase' shared library examples.
FOREACH(DEPTH_BASE_EXAMPLES_SOURCES_DIR ${DEPTH_BASE_EXAMPLES_SOURCES_DIRS})

  # Define source path of the 'DepthBase' shared library examples.
  SET(DEPTH_BASE_EXAMPLES_SRC_PATH ${DEPTH_BASE_EXAMPLES_SOURCES_DIR})

  # Define sources of the 'DepthBase' shared library examples.
  FILE(GLOB DEPTH_BASE_EXAMPLES_SOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}/${DEPTH_BASE_EXAMPLES_SRC_PATH} "${DEPTH_BASE_EXAMPLES_SRC_PATH}/*.cpp")

  # Define example targets of the 'DepthBase' shared library.
  SET(DEPTH_BASE_EXAMPLES_TARGETS)

  # Compile the 'DepthBase' shared library examples.
  FOREACH(DEPTH_BASE_EXAMPLES_SOURCE ${DEPTH_BASE_EXAMPLES_SOURCES})

    # Get example name.
    STRING(REGEX REPLACE "(.*)\\.cpp" "\\1" DEPTH_BASE_EXAMPLE ${DEPTH_BASE_EXAMPLES_SOURCE})

    # Add new local example target.
    LIST(APPEND DEPTH_BASE_EXAMPLES_TARGETS ${DEPTH_BASE_EXAMPLE})
    # Add new global example target.
    LIST(APPEND DEPTH_EXAMPLES_TARGETS ${DEPTH_BASE_EXAMPLE})

    # Build new example.
    ADD_EXECUTABLE(${DEPTH_BASE_EXAMPLE} ${DEPTH_BASE_EXAMPLES_SRC_PATH}/${DEPTH_BASE_EXAMPLES_SOURCE})

    # Set compiler flags.
    SET_PROPERTY(TARGET ${DEPTH_BASE_EXAMPLE} APPEND PROPERTY COMPILE_DEFINITIONS "__BUILD_APP__" "__CONSOLE__")

    # Link the example with the 'DepthBase' shared library.
    TARGET_LINK_LIBRARIES(${DEPTH_BASE_EXAMPLE} ${DEPTH_BASE})
    # Link the example with the 'DepthApplication' shared library.
    TARGET_LINK_LIBRARIES(${DEPTH_BASE_EXAMPLE} ${DEPTH_APPLICATION})
    # Link the example with the 'DepthApplicationConsole' static library.
    TARGET_LINK_LIBRARIES(${DEPTH_BASE_EXAMPLE} ${DEPTH_APPLICATION_CONSOLE})

    # Install the 'DepthBase' example.
    INSTALL(TARGETS ${DEPTH_BASE_EXAMPLE}
            RUNTIME DESTINATION ${DEPTH_BIN}
            OPTIONAL)

  ENDFOREACH(DEPTH_BASE_EXAMPLES_SOURCE)

ENDFOREACH(DEPTH_BASE_EXAMPLES_SOURCES_DIR)

# Add 'DepthBase' example set.
ADD_CUSTOM_TARGET(${DEPTH_BASE_EXAMPLES} DEPENDS ${DEPTH_BASE_EXAMPLES_TARGETS})
